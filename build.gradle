plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.5'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id "io.freefair.lombok" version "6.5.1"
	id "org.openapi.generator" version "6.2.1"
}

group = 'org.recipes'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

sourceSets {
	main {
		java {
			srcDir "${buildDir}/generated/src/main/java"
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	runtimeOnly 'org.postgresql:postgresql'

	// open api
	compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
	implementation 'io.swagger:swagger-annotations:1.6.8'
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.12'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mockito:mockito-core:4.8.1'
}

tasks.named('test') {
	useJUnitPlatform()
}

openApiGenerate {
	generatorName = "java"
	inputSpec = "$rootDir/src/main/resources/static/recipes-api-spec.yaml".toString()
	outputDir = "$buildDir/generated".toString()
	apiPackage = "org.recipes.api"
	invokerPackage = "org.recipes.invoker"
	modelPackage = "org.recipes.model"
	modelNameSuffix = "DTO"
	configOptions = [
			dateLibrary: "java8",
			interfaceOnly: "true",
			openApiNullable: "true",
			library: "resttemplate",
	]
}

compileJava.dependsOn tasks.openApiGenerate
